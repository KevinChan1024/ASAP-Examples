!!++
!! GRATING_USING_MODELS_01.INR
!! Title: Grating using COATING MODELS
!! Category: Simple Problem
!! Keywords: Gratings, doe, DIFFRACT, MULTIPLE, diffraction, COATINGS
!! Description: A transmission grating with three orders on
!! either side of 0 and efficiencies given by COATING MODELS.
!! Edit History: (latest first) 
!! 09/22/2011 - cp - added comments
!! 08/04/2011 - cp - creation
!!--

SYSTEM NEW
RESET

UNITS MM
WAVELENGTH 0.55 UM

FRESNEL AVE !! Don't forget this
  
MODELS 
  RAWDATA ANGLES !!PLOT
  0  0    !! This first line is required. If more incident angles needed, 
  0  0 1  !! there needs to be many sets like this provided in even increments for good interpolation.
  16 0 .3/.8  !! Dividing by the transmission at Normal incidence to normalize the data
  33 0 .1/.8
  55 0 .01/.8
  RAWDATA ANGLES 
  0  0
  0  0 1
  16 0 0
  33 0 0
  55 0 0
  RAWDATA ANGLES
  0  0    
  0  0 1
 -16 0 .25/.8  
 -33 0 .35/.8  
 -55 0 .4/.8
  RAWDATA ANGLES
   0  0 
   0  0 1 
  -16 0 0  
  -33 0 0   
  -55 0 0
COATINGS MODELS 2 2 1 1
 0 0.8 'GRT'            !! 80% transmission at Normal incidence
COATINGS MODELS 4 4 3 3 
 0 0.8 'GRT2'           !! 80% transmission at Normal incidence
SPLIT 1

SURFACES
  PLANE Y 0          
    MULTIPLE 1 0.002    
  PLANE Z 0 RECTANGLE 1   
OBJECT 'GRATING'
  INTERFACE 0 1 AIR AIR DIFFRACT 0.2 GRT 0 1 2 3  DIFFRACT 0.2 GRT2 -3 -2 -1
!! 0th order to be given only once. See item 17 in Help under INTERFACE.

!! Set up a grid and trace the rays
GRID ELLIPTIC Z -1 -4@.2 1 3
  SOURCE DIRECTION 0 0 1 
  FLUX TOTAL 100
WINDOW Y Z
PROFILES OVERLAY 'Grating Example'
TRACE PLOT COLOR 2
STATS
RETURN