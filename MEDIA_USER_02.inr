!!++
!! MEDIA_USER_02.INR
!! Title: Ring Scatterer using MEDIA USER
!! Category: Isolated Command
!! Keywords: MEDIA USER, Scatter, volume, ring
!! Description:  Using MEDIA USER with the new USERPROG.DLL for ASAP 2014. 
!! Allows the user to define a phase function for volumetric scattering
!! calculations using the subroutine USERSCAT in the USERPROG.DLL.
!! See Help under MEDIA USER for more details on usage.
!! To run this file, first copy RING.TXT from the ASAP Examples folder
!! to your Working Directory. This scatters light into a ring between 
!! 15 and 15.1 degrees. Light outside this ring is direct light or 
!! multiple-scattered light.     
!! Edit History: (latest first)
!! 1/15/2014 - gb - replaced command abbreviations with proper form
!! 12/12/2013 - cp - added header
!! 12/12/2013 - kg - creation
!!--

SYSTEM NEW 
RESET

UNITS MM
WAVELENGTH 633 NM

SPLIT 1000 MONTECARLO 
LEVEL 1000
FRESNEL AVERAGE  

SEED 

THICK=4  
THICK_GLASS=3

REF_PC=1.59 !! refractive index PC
MEDIA 
  (REF_PC) USER 1~1000000 0 0.1   'RING'  !! defines scattering model
                                         !! and reads data from file 
                                         !! RING.txt  
  1.51 'GLASS'
                                       
 
!! GEOMETRY  
SURFACES
  PLANE Z 0 ELLIPSE 5 5 
OBJECT 'SAMPLE1.FRONT'
  INTERFACE COAT BARE AIR RING   

!!
SURFACES
 PLANE Z (THICK) ELLIPSE 5 5 
OBJECT 'SAMPLE1.BACK'
  INTERFACE COAT BARE AIR RING

SURFACES
  TUBE Z 0  5 5  (THICK)  5 5 
OBJECT 'SAMPLE1.SIDE'
  INTERFACE COAT BARE AIR RING  
                            
BEAMS INCOHERENT           
GRID ELLIPTIC Z -1 -4@0.5 111 111
  SOURCE DIRECTION 0 0 1   
  FLUX TOTAL 1  

TITLE 'RAY TRACE'
MISSED ARROWS 10
WINDOW X Z 
PLOT FACETS 7 7 OVERLAY
TRACE PLOT 123

TITLE 'INTENSITY'                               
CONSIDER ONLY SAMPLE1.BACK  
STATS
WINDOW X Y     
PIXELS 99
SPOTS DIRECTION 
DISPLAY  
  FORM -4
  PICTURE
RETURN


                 
 

 
 

                      
                      




