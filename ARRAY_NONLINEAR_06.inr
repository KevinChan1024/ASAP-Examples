!!++
!! ARRAY_NONLINEAR_06.INR
!! Title: Corner Array using Functions with SEARCH
!! Category: Isolated Command
!! Keywords: ARRAY NONLINEAR, Arrays, $FCN, backlight, corner, CORNER
!! Description: A non-linear array of constant-spaced CORNER pyramids
!! using two functions and SEARCH option with trace.
!! Also see CORNER_ARRAY_01.INR.
!! Edit History: (latest first) 
!! 10/15/2013 - gb - replaced command abbreviations with proper form
!! 02/27/2012 - cp - added header
!! 02/27/2012 - kg - creation
!!--

SYSTEM NEW
RESET

UNITS MM
COATINGS PROPERTIES
  0.98 0.00 'PREFL'

!!REPEAT PARAMETERS
NX=11                           !! NUMBER OF REPEATS IN X (PAIRS)
NY=7                            !! NUMBER OF REPEATS IN Y

!!LENGTH OF CORNER
L_CORN=3                        !! AXIAL LENGTH

!! CALCULATED PARAMETERS
SIDE=L_CORN*SQRT[6]             !! LENGTH OF BASE SIDES
ANGLE_PLANE=ASIN[SQRT[1/3]]     !! ANGLE OF PLANES TO AXIS
ANGLE_RIDGE=ACOS[SQRT[1/3]]     !! ANGLE OF RIDGES TO AXIS

ROUT=L_CORN*TAN[ANGLE_RIDGE]    !! RADIUS OF OUTER (SUPERSCRIBED) CIRCLE
RIN=L_CORN*TAN[ANGLE_PLANE]     !! RADIUS OF INNER (INSCRIBED) CIRCLE

DX=ROUT+RIN                     !! ARRAY SPACING IN X FOR EACH PAIR OF CORNERS
DY=(SIDE)                       !! ARRAY SPACING IN Y FOR ROWS OF CORNERS 

$FCN DXFCN (DX)+0.00*_  !! CONSTANT SPACING
$FCN DYFCN (DY)+0.00*_  !! CONSTANT SPACING

&REG L_CORN SIDE ANGLE_PLANE ANGLE_RIDGE RIN ROUT DX DY

TX=(NX*DX)+DX                   !! TOTAL SIZE IN X
TY=(NY*DY)+DY                   !! TOTAL SIZE IN Y

SURFACES
 CORNER Z 0 LENGTH (L_CORN)
  SHIFT X -(ROUT-RIN)/2
  !!ARRAY (NX-1) (DX) 0 0  (NY-1) 0 (DY) 0 SEARCH 1  !! PREVIOUS ARRAY SYNTAX
  ARRAY NONLINEAR 1 0 0 DXFCN (NX-1)  0 1 0  DYFCN (NY-1) SEARCH 1 !!NONLINEAR ARRAY SYNTAX  
OBJECT 
 0.1 'CORN.U' COLOR 19
 INTERFACE COAT PREFL AIR AIR 
RETURN

SURFACES
 CORNER Z 0 LENGTH (L_CORN)
  SHIFT X -(ROUT-RIN)/2
  ROTATE Z 180 0 0
  SHIFT 0 (DY/2) 0
  !!ARRAY (NX-1) (DX) 0 0  (NY-1) 0 (DY) 0 SEARCH 1 !! PREVIOUS ARRAY SYNTAX
  ARRAY NONLINEAR 1 0 0 DXFCN (NX-1)  0 1 0  DYFCN (NY-1) SEARCH 1 !!ARRAY NONLINEAR SYNTAX  
OBJECT 
 0.1 'CORN.D' COLOR 20
 INTERFACE COAT PREFL AIR AIR 
RETURN

!!PLOT GEOMETRY 

TITLE 'PLOT AND RAY TRACE OF CUBE CORNER ARRAY'
WINDOW Y X
PLOT FACETS 2 2 0  OVERLAY

BEAMS INCOHERENT GEOMETRIC
WAVELENGTH 0.4 UM
GRID ELLIPTIC Z 10 -4@5 21 21 
 SOURCE DIRECTION 0 0 -1, 0 SIN[30] -COS[30]
  SHIFT 25 25 0 
 
MISSED ARROWS 5

TRACE PLOT

$VIEW

