!!++
!! LICENSE_PLATE_ANALYSIS.INR
!! Title: License Plate Bulb Simulation
!! Category: Simple Problem
!! Keywords: Analysis, license plate, bulb, library, automobile, irradiance
!! Description: Simulation of license plate illumination using 
!! bulb from Source Library. NOTE: in order to run file -
!! please copy the 211Type2.. bulb dis file indicated below from
!! Project\Samples folder to the working folder.  Change $IO
!! LIBRARY path as needed for your own machine.
!! Edit History: (latest first)
!! 10/16/2013 - gb - replaced command abbreviations with proper form
!! 01/08/2010 - cp - added header
!! 12/09/2009 - wjd - creation
!!--

!!Housekeeping
!!Delete any exisiting geometry or ray data

SYSTEM NEW
RESET

BEAMS INCOHERENT GEOMETRIC

!! Define system units and wavelengths

UNITS MILLIMETERS 'Lumens'
WAVELENGTH 550 NM

!! Define coatings

COATINGS PROPERTIES
  0 1 'TRANSMIT'
  1 0 'REFLECT'
  0 0 'ABSORB'

!! Define Models
  
MODELS
  LAMBERTIAN 0.98                            !! SCATTER MODEL FOR BULB REFLECTOR
  HARVEY 1.4 -1.5 SIN[10] 'Diffuse metal'    !! SCATTER MODEL FOR LICENSE PLATE
RETURN

SC=25.4     !! SCALE FACTOR TURNS INCHES INTO MM

!! GEOMETRY DEFINITIONS BEGIN HERE


!! SOURCE DEFINITIONS BEGIN HERE                                              
!! BAR TYPE AUTOMOTIVE BULB COMMONLY USED FOR LICENSE PLATES 
!! ACCESS BRO LIGHT SOURCE LIBRARY 
!! Change following to match your current path:
$IO LIBRARY "C:\Program Files\ASAP2009V2R1\Projects\LightSource\211TYPE2.lib"
211TYPE2_GEOM 0 0 0 +X +Y 1 0
PLACERAYS 0 0 0 +X +Y 211TYPE2_555NM_10K.DIS 555 !! must have dis file in working directory
$IO LIBRARY CLOSE    
FLUX TOTAL 158.8 lumens

!! CREATE ASAP CHARACTERS FOR LICENSE PLATE    
EDGES
  CHARACTER Z 0.1 3*SC 4*SC 'ASAP'
  SHIFT X 0.3*SC
  SHIFT Y 0.3*SC
  CHARACTER Z 0.1 3*SC 4*SC 'ASAP'
OBJECT 
  0.1 0.2 'ASAP'
  INTERFACE 1 0 
  REDEFINE COLOR 6
  SHIFT Y -135
  SHIFT Z -50
  SHIFT X -142 
******************    
!! CUTTING PLANE FOR REFLECTOR PIECE 

SURFACES
  PLANE Y 0 RECTANGLE 2@75 
**********************************
!! LICENSE PLATE

SURFACES
  PLANE Z 0 RECTANGLE 6*SC 3*SC
  SHIFT Y -80
  SHIFT Z -50
OBJECT 'LICENSE_PLATE' 
  INTERFACE .8 0
  ROUGHNESS MODEL 2 
  REDEFINE COLOR 10

!! REFLECTOR PIECE  
SURFACES
  TUBE X -2*SC 2@1.0*SC 2*SC 2@1.0*SC
OBJECT 'REFLECTOR'  
  INTERFACE 0.95 0
  ROUGHNESS MODEL 1
    BOUNDS .2 .3       
    

!! TRACE RAYS WITH OPTIONAL PLOT 
MISSED 5
$PLOT OFF
WINDOW Y X
PROFILES 0 0 -31  
WINDOW Z X
PROFILES 0 0 -31
$PLOT NORM 
WINDOW Y Z
PROFILES 0 0 -31 OVERLAY
TRACE PLOT 50
     
$VIEW

!! ANALYSIS BLOCK BEGINS HERE 
!! IRRADIANCE PATTERN IS OBSERVED HERE. CHECK STANDARDS USING CROSS-HAIRS OR VALUES COMMAND.
CONSIDER ONLY LICENSE_PLATE ASAP 

WINDOW Y X
PIXELS 75
SPOTS POSITION ATTRIBUTE 0
DISPLAY
AVERAGE 4
PICTURE
RETURN 
*******************

!! RENDERING
 
PIXELS 401 
CONSIDER ALL
RENDER 0 -30 100 RAYS 2 MODEL

STATS
RETURN


