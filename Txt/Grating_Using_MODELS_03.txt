!!++
!! GRATING_USING_MODELS_03.INR
!! Title: An HOE Grating using a COATING MODEL
!! Category: Simple Problem
!! Keywords: Gratings, DIFFRACT, hoe, doe, MULTIPLE, diffraction, COATINGS
!! Description: Based on the standard example, HOE1.inr, using a HOE
!! (Holographic Optical Element) imported from Zemax with added content
!! to show angular efficiency variation. The DOE is designed to operate
!! at a single order. An equation-based USERBSDF model with COATING MODELS
!! is used to define the angular efficiency variation of the grating.
!! Edit History: (latest first) 
!! 10/16/2013 - gb - replaced command abbreviations with proper form
!! 09/22/2011 - cp - added header
!! 09/21/2011 - jah - creation
!!--

SYSTEM NEW
RESET

!! Following geometry was translated from Zemax

UNITS MM
WAVELENGTHS 0.55 MICRON 

FRESNEL AVE !! Required

MEDIA
  0.7`-7.0  'ZMX_ALUMINUM'

COATINGS PROPERTIES
  0.0 1.0  'TRANSMIT'
  1.0 0.0  'REFLECT'
  0.0 0.0  'ABSORB'

$FCN HOLOGRM,
  FDX1=_4-_1,  FDY1=_5-_2,  FDZ1=_6-_3,  FS1=_10,
  FDX2=_7-_1,  FDY2=_8-_2,  FDZ2=_9-_3,  FS2=-1,
  FD1=FS1*SQRT(FDX1*FDX1+FDY1*FDY1+FDZ1*FDZ1),
  FD2=FS2*SQRT(FDX2*FDX2+FDY2*FDY2+FDZ2*FDZ2),
  FD1+FD2, -FDX1/FD1-FDX2/FD2, -FDY1/FD1-FDY2/FD2, -FDZ1/FD1-FDZ2/FD2 

!! Define angular variation of efficiency  
!! Use simple, arbitrary function to illustrate (a function of specular angle)
$FCN GRAT_ANG SQRT[(1-4*_3)]

MODELS  !! Angular equation
  USERBSDF GRAT_ANG      !! Model 1
RETURN

COATINGS MODELS 1 1 1 1  !! Using equation-based model above
 0.0 1.0 'GRAT_ANG_COAT'

!! Aperture stop
SURFACES
  USERFUNC 0 0 0 HOLOGRM,
    0 5 1000000000,
    0 0 -100 1
    MULTIPLE 1 0.55
SURFACES
  OPTICAL Z 0 0 0
    PARAMETERIZE -Z
    LOCAL -6@10 Z
OBJECT 'SURF1'  !! Single-order (m=1) DOE
  INTERFACE COAT "TRANSMIT" "AIR" "AIR" DIFFRACT -.2 GRAT_ANG_COAT 1

!! Detector
SURFACES
  PLANE Z 0 RECT 50
OBJECT 'IMAGE_SURF'
  INTERFACE COAT "ABSORB"  "AIR" "AIR"  
  SHIFT Z 99.950034978
RETURN

!! Trace
WAVELENGTH 0.55 UM
GRID ELLIPTIC Z 0 -4@20/2 1 5
  SOURCE DIRECTION 0 TAN[0] 1, 0 TAN[10] 1, 0 TAN[20] 1  !! three sources at three angles
  MOVE TO Z -20/2 
  FLUX TOTAL 3
WINDOW Y Z
STATS SOURCES  !! three sources at three angles {0, 10, 20} degrees

PLOT FACETS 4 4 OVERLAY 'Three sources at three angles through lens DOE'
TRACE PLOT
$VIEW 
RETURN

STATS SOURCES  !! Three sources at three angles {0, 10, 20 degrees}
!! Since the sources each had unit initial flux, these are efficiencies 
