!!++
!! SCATTER_ABG_01.INR
!! Title: Abg Scatter Model
!! Category: Isolated Command
!! Keywords: ABG, Scatter, bsdf, MODELS 
!! Description: Shows an Abg (linear shift invariant) model
!! in a graphical picture. Allows the user to vary Abg parameters
!! and see the effect. Commonly used to model scattering from
!! isotropic surface roughness. Abg is similar to the isotropic 
!! Harvey model.
!! Edit History: (latest first)
!! 10/10/2013 - gb - replaced command abbreviations with proper form
!! 03/08/2012 - cp - corrected syntax
!! 09/22/2011 - cp - modified header
!! 05/11/2011 - jj - creation
!!--

SYSTEM NEW
RESET

MODELS
!! HARVEY 0.1 -2  PLOT
  ABG 0.2  1  2  !! A B G's of the ABG model


                          
**********************************************
!! Create geometry
EDGES                  !! This is the importance edge towards which
  RECTANGLE Z 0 10 10  !! rays are scattered
    SHIFT Z 10
    
SURFACES
  PLANE Z 0 RECTANGLE 5 5 
OBJECT 'Diffuser'     !! Simple diffuser
  INTERFACE 0 0.000000001 AIR AIR  !! Keep specular rays at very low level of flux
    SCATTER MODEL 1
    TOWARDS EDGE 1 1  !! Scatter towards edge number 1, 1 ray

SURFACES
  PLANE Z 10 RECTANGLE 10 10  
OBJECT 'Detector'     !! Detector

!! Verify modles

PRINT MODELS

!! Verify geometry
PLOT FACETS 5 5 OVERLAY

**********************************************
!! Create sources
GRID RECT Z -10 -2 2 -2 2 200 200  
  SOURCE DIRECTION 0 0 1 

**********************************************
!! Trace rays
TRACE PLOT 20  !! Rays which are shown are ALL scattered rays.
               !! The number of scattered rays does not vary as the 
               !! Harvey model changes.  The flux contained in each ray
               !! DOES vary as the Harvey model is changed.
                          
**********************************************
!! Perform analysis
PIXELS 501
WINDOW Y -15 15 X -15 15 
SPOTS POSITION EVERY 7 !! Note that the spots will not change for
                       !! different Harvey parameters.  The flux contained
                       !! in different rays WILL change, however.
DISPLAY
  AVERAGE 2 2
  NORMALIZE
  FORM -6              !! This takes the log of the data.
  PICTURE
RETURN