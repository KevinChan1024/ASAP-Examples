!!++
!! REPLICATE01.INR
!! Title: Use of the REPLICATE command
!! Category: Isolated Command
!! Keywords: REPLICATE, DISPLAY 
!! Description: REPLICATE command with first element of Cooke Triplet. 
!! Edit History: (latest first) 
!! 10/10/2013 - gb - replaced command abbreviations with proper form
!! 07/25/2007 - cp - reformatted
!! 04/13/2007 - bf - creation
!!-- 

SYSTEM NEW
RESET

UNITS MM

MEDIA
  1.613  'SK4'

SURFACES
  OPTICAL Z 0 44.55 ELLIPSE 16.9 16.9
OBJECT 'L1.FRONT'    
  INTERFACE 0 1 AIR SK4
  
SURFACES
  OPTICAL Z 5 -436.6 ELLIPSE 16.9 16.9
OBJECT 'L1.BACK'    
  INTERFACE 0 1 AIR SK4

SURFACES
  TUBE Z 0 16.9 16.9   5 16.9 16.9
OBJECT 'L1.EDGE'
  BOUNDS  +.3  -.2

SURFACES
  PLANE Z  65.12    RECT 5
OBJECT 'DETECTOR'  

GRID ELLIPSE Z -1 -4@16 101  101
SOURCE DIRECTION 0 0 1
FLUX TOTAL 100

WINDOW Y Z
PLOT FACETS 7 7 0   OVERLAY
TRACE PLOT  111

CONSIDER ONLY DETECTOR
WINDOW X Y  
PIXELS 101

SPOTS POSITION

DISPLAY
  AVERAGE
  PICTURE
RETURN

DISPLAY
  REPLICATE -2
  AVERAGE
  PICTURE
RETURN
