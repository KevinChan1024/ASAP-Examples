!++
!! SCATTER_ABG_KCORRELATION_01.INR
!! Title: Comparing Abg and K-Correlation Scatter
!! Category: Simple Problem
!! Keywords: Scatter, ABG, KCORRELATION, USERBSDF, bsdf, HARVEY  
!! Description: Example of BSDF scatter calculations for
!! Lambertian and Abg, modeled as Lambertian.
!! Harvey and Abg modeled to have same TIS.
!! Abg and K-correlation modeled to have same TIS.
!! Origin is at scatter plane and propagation direction is +Z. 
!! Edit History: (latest first)
!! 07/02/2014 - cp - added XMEMORY setting to speed up virtual.pgs writing
!! 10/10/2013 - gb - replaced command abbreviations with proper form
!! 02/28/2012 - cp - added header
!! 02/28/2012 - kg - creation
!!--

SYSTEM NEW
RESET

*************************************************************************************************
!! SET UP SYSTEM VARIABLES
*************************************************************************************************

!! SYSTEM UNITS ARE MM
!! GENERAL VARIABLES

PI=4*ATAN(1)
ACF=SQRT(PI/4)              !! CONVERSION OF 1/E^2 TO ASAP DEFINITION
$FCN ALOG EXP[_]            !! ANTILOG FUNCTION FOR DISPLAY
E2M=1/25.4                     
                               
!! SPECIFIC PARAMETERS         
HWHM_WO=SQRT(LOG(0.5)/(-2)) !! HWHM TO WO RADIUS SPATIAL SCALE FACTOR 
FWHM_WO=2*HWHM_WO           !! FWHM TO WO RADIUS SPATIAL SCALE FACTOR 

!!SOURCE
WAVE=0.6328 
SRC_PWR=100
NRAYS=1E4

!!DETECTOR
SD_X=1
Z_LOC=100 
SRC_ANGLE=30 
SCAT_ANGLE=-30 
SCAT_RAYS=1

*************************************************************************************************
!! SET UP OPTICAL PROPERTIES DATABASE
*************************************************************************************************

UNITS MM 'LUMENS'
FRESNEL AVE
WAVELENGTHS   0.380000    0.430000    0.480000    0.530000    0.580000    0.630000    0.680000    0.730000    0.780000 UM 
MEDIA;        1.51066322  1.50323996  1.49825854  1.49472034  1.49209422  1.49007866  1.48849102  1.48721435  1.48617022  'EPOXY'
              0.97`6.0    0.97`6.0    0.97`6.0    0.97`6.0    0.97`6.0    0.97`6.0    0.97`6.0    0.97`6.0    0.97`6.0    'ALUM'
              1.38510079  1.38233414  1.38039668  1.37897370  1.37788648  1.37702708  1.37632706  1.37574125  1.37523883  'MGF2'

COATINGS PROPERTIES
              0.0  1.0    0.0  1.0    0.0  1.0    0.0  1.0    0.0  1.0    0.0  1.0    0.0  1.0    0.0  1.0    0.0  1.0    'TRAN'
              1.0  0.0    1.0  0.0    1.0  0.0    1.0  0.0    1.0  0.0    1.0  0.0    1.0  0.0    1.0  0.0    1.0  0.0    'REFL'
              0.0  0.0    0.0  0.0    0.0  0.0    0.0  0.0    0.0  0.0    0.0  0.0    0.0  0.0    0.0  0.0    0.0  0.0    'ABSORB'

COATINGS LAYERS 0.580
              1000 ALUM  'ALUMCOAT'
              0.25 MGF2  'MGF2_AR'                      

PIXELS 1501
MODEL (LAMB=1) 
  LAMBERTIAN 1.0 PLOT 0 30 60  'LAMBERTIAN DIFFUSER'

MODEL (ABG_LS=2)
  ABG 1 (PI-1) 0 PLOT 0 30 60 'ABG LAMBERTIAN SCATTER' 

MODEL (HARVEY=3)
	HARVEY 0.2074 -1.5 0.1864 PLOT 0 30 60  'HARVEY MODEL'  

A_ABG=0.02
B_ABG=0.1
G_ABG=1.5
SPEC_BSDF_ABG=A_ABG/B_ABG
MODEL (ABG=4)
  ABG (A_ABG) (B_ABG) (G_ABG) PLOT 0 30 60 'ABG SCATTER MODEL'

$FCN  ABG_USER _4/(_5+(_1-2*_2+_3)^(_6/2))
MODELS (ABG_USR=5)
  USERBSDF ABG_USER (A_ABG) (B_ABG) (G_ABG) !! PLOT 0 30 60 'ABG VIA USERBSDF'
  
$REG A_ABG B_ABG G_ABG SPEC_BSDF_ABG 

MODELS (KCORR=6)
  KCORRELATION 0.95 0.019 2*PI*0.5 0.6328 2 3.0 PLOT 0 30 60 'K-CORRELATION SCATTER MODEL'

RETURN
*************************************************************************************************
!! SET UP SYSTEM GEOMETRY
*************************************************************************************************

!! IMPORTANCE AREA
EDGES (IMP_AREA_1=1)
  RECT Z (Z_LOC) 2@(SD_X)  !! IMPORTANCE EDGE  
  !!ARC Z (Z_LOC) (SD_X)   !! IMPORTANCE EDGE  
    ROTATE X (SCAT_ANGLE) 0 0

!!$GO scatter
!! SCATTER OBJECT
BRANCH SCATTER

SURFACES
  PLANE Z 0 RECT 1
OBJECT 
  0.1 'SAMPLE'
  SCATTER MODELS (ABG) !!(KCORR) !!(ABG) !!(HARVEY_1) !!(LAMB) !!(HARVEY_1)
    TOWARDS EDGE (IMP_AREA_1) (SCAT_RAYS)
 
GROUP SCATTER.? 
  COLOR 18

scatter

!!$GO detector   
*************************************************************************************************
!! DETECTOR

BRANCH DETECTOR

SURFACES
  PLANE Z (Z_LOC) RECT 2@(SD_X)
OBJECT
  0.1 'PLANE' 
 
GROUP DETECTOR.?
  ROTATE X (SCAT_ANGLE) 0 0
  COLOR 10 

detector

RETURN

*************************************************************************************************
!! MACROS
*************************************************************************************************

RAY_SRC { 2
XMEMORY MIN 1E5
SEED 2000000001 QUASI
WAVELENGTH (WAVE) UM
EMITTING RECT -Z 0 0 0 (NRAYS) 0
  ROTATE X (SRC_ANGLE) 0 0
  MOVE TO Z 10
  FLUX TOTAL (SRC_PWR)
}
ENTER WAVELENGTH>
ENTER TOTAL NUMBER OF RAYS>

ANALYSIS {
!! COMPUTE THE BSDF OF THE ABG SCATTER MODEL AT 30 DEGREES AOI
TITLE 'RAY TRACE WITH ABG SCATTER MODEL'           
$RAY_SRC
WINDOW Z Y; PIXELS 201 
PROFILES OVERLAY; TRACE PLOT 1000

CONSIDER ONLY DETECTOR
STATS
$GRAB 'TOTAL' 0 2 POWER

CONSIDER ALL
!! COMPUTE BSDF=(POWER_DETECTOR/SOLID_ANGLE)/(POWER_SOURCE*COS[SCATTER_ANGLE])
BSDF=(POWER/((2*SD_X)^2/Z_LOC^2))/(SRC_PWR*COS[SCAT_ANGLE])   !!FOR RECTANGULAR IMPORTANCE AREA
!!BSDF=(POWER/((PI*SD_X^2)/Z_LOC^2))/(SRC_PWR*COS[SCAT_ANGLE])  !!FOR CIRCULAR IMPORTANCE AREA
$REG BSDF 'THE BSDF AT 30 DEGREES IS'
$SCR 4 'BSDF CALCULATION'
BSDF=(POWER_DETECTOR/SOLID_ANGLE)/(POWER_SOURCE*COS[SCATTER_ANGLE]) 
BSDF OF ABG MODEL AT 30 DEGREE AOI = \BSDF.4\
COMPARE TO ABG BSDF PLOT
\DUMMY : ?
}

BSDF_CURVE { 
!! COMPUTE K CORRELATION BSDF CURVE
EDGES (IMP_AREA_1)
  ROTATE X -(SCAT_ANGLE) 0 0

OBJECT SAMPLE
  SCATTER MODELS (KCORR) 
    TOWARDS EDGE (IMP_AREA_1) (SCAT_RAYS)
 
OBJECT DETECTOR.PLANE
  ROTATE X -(SCAT_ANGLE) 0 0
$ITER SCAT_ANGLE -60 60 -121 BSDF
 {
  $RAY_SRC
  EDGES (IMP_AREA_1)
    ROTATE X -(SCAT_ANGLE) 0 0
  OBJECT DETECTOR.PLANE
    ROTATE X -(SCAT_ANGLE) 0 0
  !!WINDOW Z Y; PIXELS 201        
  !!PROFILES OVERLAY; TRACE PLOT  !!THIS PRODUCES A LARGE NUMBER OF PLOTS
  TRACE 
  CONSIDER ONLY DETECTOR
  STATS
  $GRAB 'TOTAL' 0 2 POWER
  !! COMPUTE BSDF=(POWER_DETECTOR/SOLID_ANGLE)/(POWER_SOURCE*COS[SCATTER_ANGLE])
  BSDF=(POWER/((2*SD_X)^2/Z_LOC^2))/(SRC_PWR*COS[SCAT_ANGLE])   !!FOR RECTANGULAR IMPORTANCE AREA
  !!BSDF=(POWER/((PI*SD_X^2)/Z_LOC^2))/(SRC_PWR*COS[SCAT_ANGLE])  !!FOR CIRCULAR IMPORTANCE AREA
  $REG BSDF
  CONSIDER ALL
  RAYS 0
  EDGES (IMP_AREA_1)
    ROTATE X (SCAT_ANGLE) 0 0
  OBJECT DETECTOR.PLANE
    ROTATE X (SCAT_ANGLE) 0 0
  $REG SCAT_ANGLE
 }
$BSDF_PLOT LIT(SRC_ANGLE) K_CORRELATION
}

BSDF_PLOT {
DISPLAY BSDF_CURVE
  GRAPH '#2 BSDF PLOT FOR #1 SPECULAR ANGLE'
    'SCATTER ANGLE, DEGREES
    'BSDF=(POWER_DETECTOR/SOLID_ANGLE)/(POWER_SOURCE*COS[SCATTER_ANGLE])'
    'COMPARE TO BSDF SCATTER MODEL PLOT
} 

KCORR_ABG {
RAYS 0
SRC_ANGLE=0
CONSIDER ALL
SCAT_RAYS=1
OBJECT SAMPLE
  SCATTER MODELS (KCORR) 
    TOWARDS SPEC (SCAT_RAYS) (PI/2) 1

XMEMORY MIN
SEED 2000000001 QUASI
EMITTING RECT -Z 0 0 0 5E6 0
  ROTATE X (SRC_ANGLE) 0 0
  MOVE TO Z 10
  FLUX TOTAL 1.0

TRACE

SELECT ONLY GENERATION -1
STATS
WINDOW Y -2@1 X -2@1
PIXELS 101
SPOTS DIRECTION ATTRIBUTE 0
DISPLAY
  AVERAGE
  ANGLES
  GRAPH MAX 'K-CORRELATION INTENSITY'
RETURN
CONSIDER ALL

OBJECT SAMPLE
  SCATTER MODELS (ABG) 
    TOWARDS SPEC (SCAT_RAYS) (PI/2) 1
RAYS 0
XMEMORY MIN
SEED 2000000001 QUASI
EMITTING RECT -Z 0 0 0 5E6 0
  ROTATE X (SRC_ANGLE) 0 0
  MOVE TO Z 10
  FLUX TOTAL 1.0
TRACE
SELECT ONLY GENERATION -1
STATS
WINDOW Y -2@1 X -2@1
SPOTS DIRECTION ATTRIBUTE 0
DISPLAY
  AVERAGE
  ANGLES
  GRAPH MAX APPEND 'K-CORRELATION AND ABG INTENSITY COMPARISON'
    'ANGLE
    '1=K-CORRELATION INTENSITY
    '2=ABG INTENSITY 
}  

$TIC

$ANALYSIS
$BSDF_CURVE
$KCORR_ABG
$TIC  
RETURN

