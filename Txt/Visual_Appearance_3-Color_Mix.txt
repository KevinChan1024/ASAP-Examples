!!++
!! VISUAL_APPEARANCE_3-COLOR_MIX.INR
!! Title: Polychromatic Source using built-in CIE standard illuminant
!! Category: Simple Problem
!! Keywords: Sources, CIELUV, $GUI, CIE, cie, CHROMATICITY 
!! Description: This script was generated by the ASAP Optics Manager
!! then further edited to show additional functions.
!! This demonstrates the mixing of three sources (R,G,B)
!! and output of color analysis information.
!! A spatial CHROMATICITY analysis is done with the Y tristimulus
!! shown in the Display Viewer. The entire data set is also shown 
!! in the 3-pane CIE Viewer. 
!! The CIELUV command is then used to extract CIELUV coordinates, 
!! as well as chroma C*(uv), hue h(uv), and color difference data. 
!! The CIELUV L* is shown in the Display Viewer. The entire CIELUV 
!! data set is then shown in another instance of the 3-pane CIE Viewer.  
!! Edit History: (latest first)
!! 09/04/2018 - cp - added header and formatted
!! 07/12/2018 - jah - creation
!!--      

SYSTEM NEW
RESET
XMEMORY NORM

UNITS MILLIMETERS WATTS

IMMERSE AIR
WAVELENGTHS 0.3 0.4 0.5 0.6 0.7 0.8 MICRONS

FRESNEL AVE
ACCURACY HIGH
MISSED ARROW  16
ARROWS  1
LEVEL 1 ALL 1E-12
CUTOFF 1E-18 1000
WARNINGS -1
HALT 12 OFF 1E-06
SPLIT 1 NORM 1E-06
SAVE OFF

CIE NEW 'CIE_DEMO'

!! Variable Definitions

NRAYS_PER_SOURCE=1E5
SOURCE_TRIANGLE_SIDE=15
SOURCE_SHIFT=(SOURCE_TRIANGLE_SIDE)/3^(1/2)

!! -- Geometry Definitions --

SURFACE
  PLANE Z 0 RECTANGLE 100 100
OBJECT =TARGET
RETURN

BRANCH "SOURCE_LOCATIONS"

SURFACE
  PLANE Z 100.01 ELLIPSE 5 5
  SHIFT 0.0 1*(SOURCE_SHIFT) 0.0
OBJECT =RED_SOURCE_LOC
  REDEFINE COLOR 2
RETURN

SURFACE
  PLANE Z 100.01 ELLIPSE 5 5
  SHIFT 1*(SOURCE_SHIFT) ALONG -COS[30] -SIN[30] 0
OBJECT =GREEN_SOURCE_LOC
  REDEFINE COLOR 13
RETURN

SURFACE
  PLANE Z 100.01 ELLIPSE 5 5
  SHIFT 1*(SOURCE_SHIFT) ALONG COS[30] -SIN[30] 0
OBJECT =BLUE_SOURCE_LOC
  REDEFINE COLOR 11
RETURN
BRANCH ^

!! -- Group Place, Shift and Rotate --

BEAMS INCOHERENT GEOMETRIC

!! -- Source Definitions --

!! EmittingDiskRed:  650.0 NANOMETERS
WAVELENGTHS 0.65 MICRONS
EMITTING DISK -Z 100.0 5.0 5.0 (NRAYS_PER_SOURCE) 10.0 10.0
FLUX TOTAL 1.0 SOURCE 0.1
SELECT ONLY SOURCE 0.1
  SHIFT 0.0 1*(SOURCE_SHIFT) 0.0
RETURN

!! EmittingDiskGreen:  550.0 NANOMETERS
WAVELENGTHS 0.55 MICRONS
EMITTING DISK -Z 100.0 5.0 5.0 (NRAYS_PER_SOURCE) 10.0 10.0
FLUX TOTAL 1.0 SOURCE 0.1
SELECT ONLY SOURCE 0.1
  SHIFT 1*(SOURCE_SHIFT) ALONG -COS[30] -SIN[30] 0
RETURN

!! EmittingDiskBlue:  450.0 NANOMETERS
WAVELENGTHS 0.45 MICRONS
EMITTING DISK -Z 100.0 5.0 5.0 (NRAYS_PER_SOURCE) 10.0 10.0
FLUX TOTAL 1.0 SOURCE 0.1
SELECT ONLY SOURCE 0.1
  SHIFT 1*(SOURCE_SHIFT) ALONG COS[30] -SIN[30] 0
RETURN

SELECT ALL

!! -- Source Analysis --

STATS
STATS SOURCES

!! -- Trace --

WINDOW Y Z
PLOT FACETS 5 5 0 'Trace color mix trio' OVERLAY
TRACE PLOT 10000
$VIEW

!! -- Analysis of Trace --

CONSIDER ONLY TARGET

WINDOW Y X

PIXELS 51 1

CHROMATICITY IRRADIANCE 1964 5

!! Open the tristimulus Y in the Display Viewer
!!
!! Description of CHROMATICITY distribution planes
!! 1ST: tristimulus X  , 2ND: tristimulus Y , 3RD: tristimulus Z ,
!! 4TH: x  , 5TH: y , 
!! 6TH: u  , 7TH: v ,
!! 8TH: u' , 9TH: v',
!! 10TH: CCT
!!
DISPLAY "ciexyzxy_CIE_DEMO" 2ND
 PICTURE 'Tristimulus Y'
RETURN

CIELUV  CIE D65

!! Open the CIELUV L* in the Display Viewer
!!
!! Description of CIELUV distribution planes
!! 1ST: L* 
!! 2ND: u* 
!! 3RD: v*
!! 4TH: Chroma C*(uv)
!! 5TH: Hue angle h(uv)
!! 6TH: Total color difference 
!!
DISPLAY "cieluv_CIE_DEMO" 1ST
  PICTURE 'CIELUV L*'
RETURN

!! open CHROMATICITY results in CIE Viewer
$GUI CalculateCIE "ciexyzxy_CIE_DEMO.dis"
!! open CIELUV results in CIE Viewer
$GUI CalculateCIE "cieluv_CIE_DEMO.dis"

!! Open the current CIE result file in Visual Appearance Display Viewer
$GUI VisualAppearance
RETURN
