!!++
!! ITER_MULTIPLE_VAR_GRAPHING01.INR
!! Title: Plotting Multiple Iter Results in one Graph
!! Category: Simple Problem
!! Keywords: Analysis, $ITER, plot, GRAPH, $SCR  
!! Description: A way to run multiple iterations on several
!! variables, storing the data in one display file so they can
!! be graphed separately or as a series.
!! Edit History: (latest first) 
!! 01/14/2009 - cp - added header
!! 02/19/2004 - jah - creation
!!--

SYSTEM NEW
RESET

XMEMORY MIN

!!MUST KNOW NUMBER OF ROWS AND COLUMNS
ROWS=6
COLUMNS=4 


$ITER XX 0 3 -(ROWS)    !! number of steps must agree with rows
{

AZ=(XX)
ER=(XX)+1
TY=(XX)^2+2
UI=(XX)^3+3


!!WRITES TO TEXT FILE ON EACH PASS
$ECHO NONE
$IF (XX) EQ 0 THEN  
  $IO OUTPUT FOO.TXT -FILE(10)   !! FIRST ITERATION
$ELSE  
  $IO OUTPUT FOO.TXT +FILE(10)   !! ALL SUCCESSIVE ITERATIONS
$END
$SCR 1
\AZ.3   \ \ER.3   \ \TY.3   \ \UI.3   \
$IO OUTPUT CLOSE
$ECHO ALL
}
RETURN

!!WRITES TEXT DATA TO DISPLAY FILE
DISPLAY -56    !! open empty display file
X 0 SPOTS Y 1 2@(ROWS) Z 1 2@(COLUMNS)
$READ FOO.TXT

!!READS AND VIEWS DISPLAY FILE CONTENTS
DISPLAY 56
  TRANSPOSE   !! to organize by variable, not iteration
  PICTURE     !! overview of data
  GRAPH 1 'LINE 1'  !! plot variables individually
  GRAPH 2 'LINE 2' 
  GRAPH 3 'LINE 3' 
  GRAPH 4 'LINE 4'  
  !! plot data for all variables on the same plot
  !! (limited to 10 plots per chart)
  GRAPH 1 2 3 4 'Data series: linear, linear with intercept, squared, cubed'
RETURN

!! Expected result: DISPLAY file with the numbers:
!!  0.000     1.000     2.000     3.000
!!  0.600     1.600     2.360     3.216
!!  1.200     2.200     3.440     4.728
!!  1.800     2.800     5.240     8.832
!!  2.400     3.400     7.760    16.824
!!  3.000     4.000    11.000    30.000
!! Obviously, it's just an example...
      
