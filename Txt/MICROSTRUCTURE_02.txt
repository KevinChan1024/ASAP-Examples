!!++
!! MICROSTRUCTURE_02.INR
!! Title: Bicycle Tail Light and Reflector
!! Category: Isolated Command
!! Keywords: MICROSTRUCTURE, unit cell, backlight, lightguide, Arrays, bumps, led
!! Description:  Bicycle tail light and reflector with corner
!! cube microstructure.
!! Edit History: (latest first)
!! 10/10/2013 - gb - replaced command abbreviations with proper form
!! 06/04/2013 - cp - added header and formatted
!! 05/01/2013 - rs - creation
!!-- 

SYSTEM NEW
RESET

UNITS MM
WAVE 625 NM

MEDIA
  1.576 'POLYCARB'
  1.45  'EPOXY'
RETURN

!!===================================
!! Define a unit cell (objects imported from cad)
!!===================================

!! Bounding edge
EDGES
  RECTANGLE Z 0 2@1
RETURN

!! Unit cell objects that fit within the bounding edge
CURVES
  POINTS 0.1282804E-3 -.2372589E-1 -.4427771    REL; $FAST 12 1
 -.0003848 -.0716221  0.  1. -.0825737 0.0716221  0.  1. -.0003848 -.0716221  0.  1.
 0.0825737 0.0711777  0.  1.
  PATCH
OBJECT =CC.1


CURVES
  POINTS -.3541322    0.2047546    -.2213885    REL; $FAST 12 1
 -.2716868 -.4705753 0.2213885  1. 0.2716868 0.4705753 0.2213885  1. -.2716868 -.4705753 0.2213885  1.
 0.2716868 -.1568584 -.2213885  1.
  PATCH
OBJECT =CC.2


CURVES
  LINE -.5436301    -.4090648    -.4427770E-9 -.2565612E-3 -.9534795E-1 -.4427771   
   SWEEP DIR 0.1651481    -0.4976679  0.8673677  0.0000000
OBJECT =CC.3


CURVES
  LINE 0.8270203E-1 0.6748855    -.4427770E-9 0.8270203E-1 0.4745179E-1 -.4427771   
   SWEEP DIR 0.1651481    -0.9999964  0.0026908  0.0000000
OBJECT =CC.4


CURVES
  POINTS 0.3543888    0.2043102    -.2213885    REL; $FAST 12 1
 0.2716868 -.4705753 0.2213885  1. -.2716868 -.1568584 -.2213885  1. 0.2716868 -.4705753 0.2213885  1.
 -.2716868 0.4705753 0.2213885  1.
  PATCH
OBJECT =CC.5


CURVES
  LINE 0.5431169    -.4090648    -.4427770E-9 -.2565602E-3 -.9534795E-1 -.4427771   
   SWEEP DIR 0.1651481     0.5023285  0.8646769  0.0000000
OBJECT =CC.6


CURVES
  POINTS -.2565607E-3 -.2522064    -.2213885    REL; $FAST 12 1
 0.5433735 -.1568584 0.2213885  1. 0.0000000 0.1568584 -.2213885  1. 0.5433735 -.1568584 0.2213885  1.
 -.5433735 -.1568584 0.2213885  1.
  PATCH
OBJECT =CC.7


CURVES
  LINE -1.000000    -1.000000    0.  1.000000    -1.000000    0.
  LINE  1.000000    -1.000000    0.  1.000000     1.000000    0.
  LINE  1.000000     1.000000    0. -1.000000     1.000000    0.
  LINE -1.000000     1.000000    0. -1.000000    -1.000000    0.
  COMPOSITE GAP -1 -1
OBJECT =BORDER
CURVES
  LINE -.8244547E-1 0.6753299    0. 0.8270203E-1 0.6748855    0.
  LINE 0.8270203E-1 0.6748855    0. 0.6260755    -.2662651    0.
  LINE 0.6260755    -.2662651    0. 0.5431169    -.4090648    0.
  LINE 0.5431169    -.4090648    0. -.5436301    -.4090648    0.
  LINE -.5436301    -.4090648    0. -.6258190    -.2658207    0.
  LINE -.6258190    -.2658207    0. -.8244547E-1 0.6753299    0.
  COMPOSITE GAP -1 -1
OBJECT .1; BOUND ,
.1

$DO 1 7
{
OBJECT CC.?
  INTERFACE COAT BARE AIR POLYCARB
  REDEFINE COLOR 11
RETURN
}

OBJECT BORDER
  INTERFACE COAT BARE AIR POLYCARB
  REDEFINE COLOR 17
  FACETS 11 11 0
RETURN
!! End unit cell

!!===================================
!! Define a microstructure model
!!===================================
MODEL
  MICROSTRUCTURE Z 1, CC.? BORDER
RETURN

!!===================================
!! Define the bicycle lens
!!===================================

!! Exterior 
SURFACES
  PLANE Z 4
  ELLIPSOID 3@15, 3@0
OBJECT 'EXTERIOR'
  BOUNDS .2
  SHIFT 0 0 -4
  INTERFACE COAT BARE AIR POLYCARB
  REDEFINE COLOR 2
RETURN

!! Interior 
SURFACES
  PLANE Z 4
  ELLIPSOID 3@13, 3@0
OBJECT 'INTERIOR'
  BOUNDS .2
  SHIFT 0 0 -4
  SCATTER MODEL 1	!! Applies microstructure to the interior  
 !! FACETS 11 11
  REDEFINE COLOR 20
RETURN

!! Edge
SURFACES
  ELLIPSOID 3@15, 3@0
    SHIFT 0 0 -4
  ELLIPSOID 3@13, 3@0
    SHIFT 0 0 -4
  PLANE Z 0 ELLIPSE 2@15 10/15
OBJECT 'EDGE'
  BOUNDS -.3 .2
  INTERFACE COAT BARE AIR POLYCARB
  REDEFINE COLOR 1
RETURN
!! End the bicycle lens

!!===================================
!! Floor
!!===================================
SURFACE
  PLANE Z -.005 ELL 2@16 12/16
OBJECT 'FLOOR.1'
  REDEFINE COLOR 1
RETURN

SURFACE
  TUBE Z -5 2@16 -.005 2@16
OBJECT 'FLOOR.2'
  REDEFINE COLOR 1
RETURN

SURFACES
  PLANE Z -5 ELL 16
OBJECT 'FLOOR.3'
  REDEFINE COLOR 1
RETURN

SURFACES
  TUBE Z -4 2@12 -.005 2@12
OBJECT 'FLOOR.4'
  REDEFINE COLOR 1
RETURN

SURFACES
  PLANE Z -4 ELL 12
OBJECT 'FLOOR.5'
  REDEFINE COLOR 1
RETURN
!! End floor

!!===================================
!! LED geometry
!!===================================
$DO 1 6
{

!! LED ?
SURFACES
  PLANE Z 0
  ELLIPSOID 3@1 3@0
OBJECT 'LED.TOP.?'
  BOUNDS .2
  SHIFT -3 0 0
  ROTATE Z (?-1)*60 0 0
  INTERFACE COAT BARE AIR EPOXY
  REDEFINE COLOR 3
RETURN

SURFACES
  TUBE Z -1.5 2@1 0 2@1
OBJECT 'LED.TUBE.?'
  SHIFT -3 0 0
  ROTATE Z (?-1)*60 0 0
  INTERFACE COAT BARE AIR EPOXY
  REDEFINE COLOR 3
RETURN

SURFACES
  PLANE Z -1.5 ELLIPSE 1
OBJECT 'LED.BOTTOM.?'
  SHIFT -3 0 0
  ROTATE Z (?-1)*60 0 0
  REDEFINE COLOR 11
RETURN

SURFACES
  TUBE Z -1.2 2@.4 -0.8 2@.8
OBJECT 'REF.?'
  INTERFACE .8
  SHIFT -3 0 0
  ROTATE Z (?-1)*60 0 0
  REDEFINE COLOR 1
RETURN
  }
!! End LED geometry

!!===================================
!! Render the object and overlay
!! the rendering with a representation
!! of the unit cells
!!===================================
$IO VECTOR REWIND
WINDOW Y X
PLOT FACETS 9 9 MICROSTRUCTURE
$VIEW
!! End rendering 

!! Define LED sources
NRAYS=10000
IMMERSE EPOXY
EMITTING RECT Z -1.2 2@.24 (NRAYS)
  SHIFT -3 0 0
  ROTATE Z 0 0 0

EMITTING RECT Z -1.2 2@.24 (NRAYS)
  SHIFT -3 0 0
  ROTATE Z 60 0 0

EMITTING RECT Z -1.2 2@.24 (NRAYS)
  SHIFT -3 0 0
  ROTATE Z 120 0 0

EMITTING RECT Z -1.2 2@.24 (NRAYS)
  SHIFT -3 0 0
  ROTATE Z 180 0 0

EMITTING RECT Z -1.2 2@.24 (NRAYS)
  SHIFT -3 0 0
  ROTATE Z 240 0 0

EMITTING RECT Z -1.2 2@.24 (NRAYS)
  SHIFT -3 0 0
  ROTATE Z 300 0 0
RETURN 

FLUX TOTAL 1

!! Trace rays from the LEDs
SPLIT 1 .49
FRESNEL AVE
ARROWS OFF
WINDOW Y Z
PIXELS 501
PROFILES 2@.001 1 OVERLAY
TRACE PLOT 1000

!! Calculate the outbound intensity
CONSIDER ONLY EXTERIOR
WINDOW Y -1 1 X -1 1
PIXELS 101
SPOTS DIRECTION ATTRIBUTE 0
DISPLAY
  PICTURE 'LAMP INTENSITY'
RETURN

!!==================================
!! CALCULATE REFLECTED INTENSITY 
!!==================================

!! Define a source of rays
CONSIDER ALL
RAYSET 0
IMMERSE AIR
EMITTING DISK -Z 0 2@15 1000000 .05
  FLUX TOTAL 3.1416*15^2		!! Normalize to unit irradiance
  MOVE TO Z 25
RETURN

!! Trace the rays
WINDOW Y Z
WINDOW 2
ARROWS OFF
SPLIT 1 .49
FRESNEL AVE
PLOT FACETS 9 9 OVERLAY
TRACE PLOT 1000

!!
!! Calculate refleted intensity
!!
CONSIDER ONLY EXTERIOR
WINDOW Y -.02 .02 X -.02 .02
PIXELS 101
SPOTS DIRECTION ATTRIBUTE 0
DISPLAY
  PICTURE 'REFLECTED INTENSITY'
RETURN
