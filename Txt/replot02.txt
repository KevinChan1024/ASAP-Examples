!!++
!! REPLOT02.INR
!! Title: REPLOT of a GRIN trace
!! Category: Isolated Command
!! Keywords: REPLOT, GRIN, FUNCTION
!! Description: Using the REPLOT command on
!! a trace of a 2 period pitch GRIN rod.
!! Edit History: (latest first)
!! 10/10/2013 - gb - replaced command abbreviations with proper form
!! 04/11/2013 - cp - modified keywords
!! 10/11/2000 - cp - creation
!!--

SYSTEM NEW
RESET

A=0.247  A0=-A*A/2  !! Quadratic index term
UNITS MM
WAVELENGTHS 0.6328 UM
FUNCTION
  GENERAL 0 0 0
    C 1
    X2 (A0)
    Y2 (A0)
MEDIA 
  1.5637 GRIN surf 1 exponent 2 stepsize .1 mm 'SELFOC'
COATINGS PROPERTIES
  0 1 'TRANS'
SURFACES
  PLANE Z 0 ELLIPSE 2
  OBJECT 'FIBER_FACE 
    INTERFACE COAT TRANS AIR SELFOC
SURFACES
  TUBE Z 0 2@2 20 2@2
  OBJECT 'FIBER_BODY' 
    INTERFACE COAT TRANS AIR SELFOC
SURFACES
  PLANE Z 20 ELLIPSE 2
  OBJECT 'FIBER_END' 
    INTERFACE COAT TRANS AIR SELFOC
SURFACES
  PLANE Z 29 RECTANG 4
  OBJECT 'DETECTOR' 

BEAMS INCOHERENT GEOMETRIC
GRID ELLIPTIC Z 0 -4@.75 1 10
  SOURCE POSITION 0 0 -1
  MOVE TO Z -1

WINDOW Y -5 5  Z -2 30
PIXELS 127
PROFILES 'YZ SLICE THROUGH FIBER'

WINDOW X Z
PROFILES 'XZ SLICE THROUGH FIBER'

CONSIDER ONLY 2 4
WINDOW X Z
OBLIQUE
PLOT LIMITS 'PLOT LIMIT BOX BOUNDARIES'

CONSIDER ALL
WINDOW Y Z
OBLIQUE OFF
TRACE PLOT 'PLOT RAYS'
OBLIQUE
WINDOW Y X
WINDOW 1.2
REPLOT 'NIPPON SLS 2.0 GRADIENT INDEX ROD'
RETURN