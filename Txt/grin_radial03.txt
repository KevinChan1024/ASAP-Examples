!!++
!! GRIN_RADIAL03.INR
!! Title: Radial GRIN Using a SECH Function
!! Category: Isolated Command
!! Keywords: GRIN, MEDIA, FUNCTION, radial, fiber, SECH, hyperbolic
!! Description: Example of a GRIN fiber using a Hyperbolic Secant function.
!! Traces a collimated source of rays through the material
!! and plots slices through the rod.
!! Edit History: (latest first) 
!! 10/16/2013 - gb - replaced command abbreviations with proper form
!! 12/07/2009 - cp - added header
!! 12/04/2009 - jah - replaced quadratic radial GRIN from grin_radial02.inr
!!-- 

SYSTEM NEW
RESET

UNITS MM
WAVELENGTHS 0.6328 UM

$FCN SECH_AR  R=SQRT[_1^2+_2^2],
              A=_4,
              [2/(EXP(A*R)+EXP(-A*R))] 

$EVAL _1 -10 10 -51 _2 -10 10 -51 _4 0.5 0.5 -1 SECH_AR  
DISPLAY EVAL.DIS
  PICTURE 'sech( )'
RETURN

A=0.247  A0=-A*A/2  !! Quadratic index term
FUNCTION
  GENERAL 0 0 0
    C 1
    X2 (A0)
    Y2 (A0) 
  USERFUNC 0 0 0 SECH_AR 0.5 
MEDIA 
  !! 1.5637 GRIN surf 1 exponent 2 stepsize .1 mm 'SELFOC'
  1.5637 GRIN surf 2 exponent 1 stepsize .1 mm 'SELFOC'

COATINGS PROPERTIES
  0 1 'TRANS'
SURFACES
  PLANE Z 0 ELLIPSE 2
  OBJECT 'FIBER_FACE 
    INTERFACE COAT TRANS AIR SELFOC
SURFACES
  TUBE Z 0 2@2 20 2@2
  OBJECT 'FIBER_BODY' 
    INTERFACE COAT TRANS AIR SELFOC
SURFACES
  PLANE Z 20 ELLIPSE 2
  OBJECT 'FIBER_END' 
    INTERFACE COAT TRANS AIR SELFOC
SURFACES
  PLANE Z 29 RECTANGLE 4
  OBJECT 'DETECTOR' 

BEAMS INCOHERENT GEOMETRIC
GRID ELLIPSE Z 0 -4@.75 1 10
  SOURCE POSITION 0 0 -1
  MOVE TO Z -1

WINDOW Y -5 5  Z -2 30
PIXELS 127
PROFILES 'YZ SLICE THROUGH FIBER'
WINDOW X Z
PROFILES 'XZ SLICE THROUGH FIBER'
CONSIDER ONLY 2 4
WINDOW X Z
OBLIQUE
PLOT LIMITS 'PLOT LIMIT BOX BOUNDARIES'
CONSIDER ALL
WINDOW Y Z
OBLIQUE OFF
TRACE PLOT 'PLOT RAYS'
OBLIQUE 
WINDOW Y X
WINDOW 1.2
!! Right-click plot to end replot
REPLOT 'SECH FUNCTION GRADIENT INDEX ROD' 
RETURN
